>>>markdown
# Steven Spielberg
What did he direct? When? Who does he commonly work with and on what?
>>>malloy
import {movies} from "imdb.malloy" 
>>>markdown
## Map of Steven Spielberg and the people that worked on them
Build a list of title/people ids of all the people that work on movies that spielberge worked on.
>>>malloy
query: spielberg_people is movies->{
  where: principals.people.primaryName ~ r'Steven Spielberg'
  group_by: tconst, principals.people.nconst
}

run: spielberg_people
>>>markdown
## People that work with Steven Spielberg 
>>>malloy
run: movies -> {
  // join in the query above.
  extend: {
    join_many: spielberg_people on spielberg_people.tconst=tconst
  }
  // filter the list to speilberg movies
  //  and remove spielberg from the list
  where: 
    spielberg_people.tconst != null  
    and principals.people.nconst != spielberg_people.nconst

  group_by: principals.people.primaryName
  aggregate: title_count

  aggregate: `years` is concat(min(startYear)::string, 
      '-', max(startYear)::string)
  limit: 20
  order_by: title_count desc
  nest: by_job_list is by_job_category
  nest: title_list is by_title
}
>>>markdown
## Steven Spielberg dashboard
>>>malloy
# dashboard
run: movies -> titles_dashboard + { 
  extend: {
    join_many: spielberg_people on spielberg_people.tconst=tconst
  }
  where: spielberg_people.tconst != null
}
